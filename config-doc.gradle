/*
 * ************************************************************
 * 文件：config-doc.gradle  模块：ElegantBus  项目：ElegantBus
 * 当前修改时间：2020年06月16日 23:43:38
 * 上次修改时间：2020年06月16日 23:36:41
 * 作者：Cody.yi   https://github.com/codyer
 *
 * 描述：ElegantBus
 * Copyright (c) 2020
 * ************************************************************
 */

import org.gradle.internal.jvm.Jvm

//编译groovy代码时采用 UTF-8
tasks.withType(GroovyCompile) {
    groovyOptions.encoding = "MacRoman"
}

//编译JAVA文件时采用UTF-8
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.convention('sources')
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    source += android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    if (Jvm.current().getJavaVersion() == JavaVersion.VERSION_1_8) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    failOnError false
}

// 打包文档jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.convention('javadoc')
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/*在javadoc task执行之前先添加第三方的依赖进来，否则会找不到相关的jar包*/
afterEvaluate {
    javadoc.source += "$buildDir/generated/aidl_source_output_dir/"
//    javadoc.source += compileReleaseAidl.sourceOutputDir.get().asFileTree
    javadoc.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
}

javadoc {
    options {
        // 语言
        locale 'en_US'
        encoding 'UTF-8'
        charSet 'UTF-8'
        links "http://docs.oracle.com/javase/8/docs/api"
    }
}