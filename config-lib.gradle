/*
 * ************************************************************
 * 文件：config-lib.gradle  模块：ElegantBus  项目：ElegantBus
 * 当前修改时间：2020年06月18日 13:38:08
 * 上次修改时间：2020年06月18日 08:29:29
 * 作者：Cody.yi   https://github.com/codyer
 *
 * 描述：ElegantBus
 * Copyright (c) 2020
 * ************************************************************
 */


apply plugin: 'java-library'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

//指定编译的编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
}

task lint {}

if (!isNoDoc) {//本地仓库不需要注释
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveBaseName.set(project.parent.name)
        archiveAppendix.set(project.name)
        archiveClassifier.convention('sources')
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveBaseName.set(project.parent.name)
        archiveAppendix.set(project.name)
        archiveClassifier.convention('javadoc')
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    javadoc {
        options {
            // 语言
            locale 'en_US'
            encoding 'UTF-8'
            charSet 'UTF-8'
            links "http://docs.oracle.com/javase/8/docs/api"
        }
    }
}

if (isLocalMaven) {
    apply from: "${project.rootDir}/config-local-maven.gradle"
} else {
    apply plugin: 'com.github.dcendents.android-maven'
    group = 'com.github.codyer'
}